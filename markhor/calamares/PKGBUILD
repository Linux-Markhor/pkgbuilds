#https://github.com/calamares/calamares/releases

pkgname=markhor-calamares
_pkgname=calamares
pkgver=3.2.40
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares/calamares/releases"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'kio' 'solid' 'yaml-cpp' 'kpmcore>=4.2.0' 'mkinitcpio-openswap'
         'boost-libs' 'ckbcomp' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'plasma-framework'
         'qt5-xmlpatterns' 'squashfs-tools' 'libpwquality' 'efibootmgr' 'icu')
conflicts=('calamares')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=("$_pkgname-$pkgver::$url/download/v$pkgver/$_pkgname-$pkgver.tar.gz"
	"ucode_main.py"
	"ucode_module.desc"
	"dm_main.py"
	"packages_main.py"
	"umount_module.desc"
	"markhor.desktop"
	"markhor-debugging.desktop"
	"calamares_polkit")
	
sha256sums=('2499011f9b581437766186db545febbaf019b4b5d14f859631d5ddc5927aee4a'
            '24f4f408f65de55f0ff07ca44a8c70f9b66f0662f3e8fdef1f619390ad0896d1'
            '830b6e2efaf794dd3c0503dc990249742ac97b8a92a414b2c1cdc9d562864aed'
            '00b0e2bfb7223e7d3f7514ea9bff910de4e7dcfe5db34873b57eb215fd6abe9c'
            'c171439b4cfcb11e0aa4dd0772d6ed2fcf64bc1c60ade3e0f053788f9eecb027'
            '0a9eb0362012374f977a620b9f9f7e23befa8c9695b78b0f77d2d535877f9104'
            '267ab3852efa4ee32665b10e8b2e372d88f4acc0d87a6da3b84e1ea9e99cf255'
            '878199d874997838ed224ea41e2cfcbc52825fc5409f68fb569094a7d2d80fb4'
            '966c5f0834039dc6a7529e75f70417ba2510b1e643ffb49fd25855ce9dc244b4')



prepare() {

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/${_pkgname}-${pkgver}/CMakeLists.txt"
	sed -i -e 's/# DEBUG_FILESYSTEMS/DEBUG_FILESYSTEMS/' "$srcdir/${_pkgname}-${pkgver}/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "$srcdir/${_pkgname}-${pkgver}/src/modules/fstab/main.py"

	# add pkgrelease to patch-version
	cd ${_pkgname}-${pkgver}
	_patchver="$(cat CMakeLists.txt | grep -m3 -e CALAMARES_VERSION_PATCH | grep -o "[[:digit:]]*" | xargs)"
	sed -i -e "s|CALAMARES_VERSION_PATCH $_patchver|CALAMARES_VERSION_PATCH $_patchver-$pkgrel|g" CMakeLists.txt

	install -Dm644 "${srcdir}/"ucode_main.py ${srcdir}/$_pkgname-$pkgver/src/modules/ucode/main.py
	install -Dm644 "${srcdir}/"ucode_module.desc ${srcdir}/$_pkgname-$pkgver/src/modules/ucode/module.desc
	install -Dm644 "${srcdir}/"dm_main.py ${srcdir}/$_pkgname-$pkgver/src/modules/displaymanager/main.py
	install -Dm644 "${srcdir}/"umount_module.desc ${srcdir}/$_pkgname-$pkgver/src/modules/umount/module.desc
	install -Dm644 "${srcdir}/"packages_main.py ${srcdir}/$_pkgname-$pkgver/src/modules/packages/main.py

}

build() {
	cd $_pkgname-$pkgver

	mkdir -p build
	cd build
	cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PYTHONQT=OFF \
	-DWITH_KF5DBus=OFF \
	-DBoost_NO_BOOST_CMAKE=ON \
	-DWEBVIEW_FORCE_WEBKIT=OFF \
	-DSKIP_MODULES="webview tracking interactiveterminal initramfs \
	initramfscfg dracut dracutlukscfg \
	dummyprocess dummypython dummycpp \
	dummypythonqt services-openrc \
	keyboardq localeq welcomeq"
	make
}

package() {
	cd ${_pkgname}-${pkgver}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/markhor.desktop" "$pkgdir/usr/share/applications/markhor.desktop"
	install -Dm644 "$srcdir/markhor-debugging.desktop" "$pkgdir/usr/share/applications/markhor-debugging.desktop"	
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	rm "$pkgdir/usr/share/applications/calamares.desktop"
}
